// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    //Valor do loop incrementado
    Inc16(in=LoopReg, out=RegInc);

    // Mux para escolher se o input vai ser o feedback do registrador sem incremento ou incrementado pelo INC16
    Mux16(a=LoopReg, b=RegInc, sel=inc,out=incOut);

    // Mux para o load gravar o valor atual da entrada no contador
    Mux16(a=incOut, b=in, sel=load, out=LoadOut);

    // Mux para selecionar se reseta o contador ou nao
    Mux16(a=LoadOut, b[0..15]=false, sel=reset, out=OutReset);

    //Registrador para guardar o valor do contator
    Register(in=OutReset, load=true, out=out,out=LoopReg);
    }
